    PUBLIC XRAND

; this is a 16 bit xorshift psudorandom number generator discovered 
; by George Marsaglia.  It's been converted for use in Pascal/MT+.
; we simply add the return vectors to beginig and end of the subroutine
; so that it can be directly included in pascal source with:
;     EXTERNAL PROCEDURE XRAND; 
; transcoded to pure 8080 assembler by D. Collins 2020, use at your own 
; risk. free to distribute and use.  Please see LICENSE in repository
; for more details.

XRAND:
     POP  B          ; return address pulled off the stack

     LXI  H,1        ; seed can be mod / read w/ a pointer from pascal
                     ; it is located at XRAND+2 bytes, and is a 16 bit word 

     MOV  A,H        ; the magic sauce 
     RAR
     MOV  A,L
     RAR
     XRA  H
     MOV  H,A
     MOV  A,L
     RAR
     MOV  A,H
     RAR
     XRA  L
     MOV  L,A
     XRA  H 
     MOV  H,A
     SHLD (XRAND+2)  ; yes,the code self modifies the seed data for efficency
    
     PUSH B          ; return to this addres to the stack 
     RET 
     
     END

